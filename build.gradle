plugins {
    id "architectury-plugin" version "3.4-SNAPSHOT"
    id "dev.architectury.loom" version "1.1-SNAPSHOT" apply false
    id "org.cadixdev.licenser" version '0.6.1' apply false
}

architectury {
    minecraft = rootProject.minecraft_version
}

configure(subprojects.findAll {it.name != 'tools'}) {
    apply plugin: "dev.architectury.loom"

    loom {
        silentMojangMappingsLicense()
    }

    dependencies {
        minecraft "com.mojang:minecraft:${rootProject.minecraft_version}"
        mappings loom.layered {
            officialMojangMappings()
            parchment("org.parchmentmc.data:parchment-$rootProject.parchment_version@zip")
        }
    }
}

subprojects {
    apply plugin: "org.cadixdev.licenser"

    license {
        header = rootProject.file("HEADER")
        include("**/*.java")
    }

    repositories {
        mavenCentral()
        maven { url = 'https://maven.parchmentmc.org' }
        maven { url = "https://oss.sonatype.org/content/repositories/snapshots/" }
        maven { url 'https://jitpack.io' }
        mavenLocal()
    }
}

allprojects {
    apply plugin: "java"
    apply plugin: "architectury-plugin"
    apply plugin: "maven-publish"

    archivesBaseName = rootProject.archives_base_name
    version = rootProject.mod_version
    group = rootProject.maven_group

    repositories {
        mavenLocal()
    }

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
        options.release = 17
    }

    java {
        withSourcesJar()
    }
}
